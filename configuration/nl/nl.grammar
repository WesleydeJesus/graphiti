/*
 * Copyright (c) 2008, IETR/INSA of Rennes
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the IETR/INSA of Rennes nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

%header%

GRAMMARTYPE = "LL"

DESCRIPTION = "A grammar for NL."

AUTHOR      = "Matthieu Wipliez"
VERSION     = "1.0"
DATE        = "November, 4th, 2008"

LICENSE     = "Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

  * Redistributions of source code must retain the above copyright notice,
    this list of conditions and the following disclaimer.
  * Redistributions in binary form must reproduce the above copyright notice,
    this list of conditions and the following disclaimer in the documentation
    and/or other materials provided with the distribution.
  * Neither the name of the IETR/INSA of Rennes nor the names of its
    contributors may be used to endorse or promote products derived from this
    software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS 'AS IS'
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
SUCH DAMAGE."

COPYRIGHT   = "Copyright (c) 2008, IETR/INSA of Rennes. All rights reserved."


%tokens%

TOK_ALL       = "all"
TOK_END       = "end"
TOK_ENTITIES  = "entities"
TOK_IMPORT    = "import"
TOK_MULTI     = "multi"
TOK_MUTABLE   = "mutable"
TOK_NETWORK   = "network"
TOK_STRUCTURE = "structure"
TOK_VAR       = "var"

ARROW              = "->"
COLON              = ":"
COLON_EQUAL        = ":="
COMMA              = ","
DIFFERENT          = "!="
DIV                = "/"
DOT                = "."
DOUBLE_DASH_ARROW  = "-->"
DOUBLE_EQUAL_ARROW = "==>"
DOUBLE_DOT         = ".."
DOUBLE_COLON       = "::"
EQUAL              = "="
GE                 = ">="
GT                 = ">"
ID                 = <<[a-zA-Z_$\\][a-zA-Z0-9_$\\]*>>
LBRACE             = "{"
LBRACKET           = "["
LPAREN             = "("
LE                 = "<="
LT                 = "<"
MINUS              = "-"
NUMBER             = <<[0-9]+>>
PLUS               = "+"
RBRACE             = "}"
RBRACKET           = "]"
RPAREN             = ")"
SEMICOLON          = ";"
SHARP              = "#"
STRING             = <<"([^\\"]*(\\[tnr\\"])?)*">>
TIMES              = "*"

LINE_COMMENT       = <<//[^\r\n]*(\r|\n|\r\n)>> %ignore%
MULTI_LINE_COMMENT = <</\*([^*]|\*[^/])*\*/>> %ignore%
WHITESPACE         = <<[ \t\n\r]+>> %ignore%

%productions%

Network = TOK_NETWORK QualifiedId (LBRACKET TypePars? RBRACKET)?
  LPAREN Parameters? RPAREN
  PortDecls? DOUBLE_EQUAL_ARROW PortDecls? COLON
  Import* VarDeclSection?
  EntitySection? StructureSection?
  TOK_END ;

///////////////////////////////////////////////////////////////////////////////
// IMPORTS

Import = TOK_IMPORT ImportRest SEMICOLON ;

ImportRest = TOK_ALL QualifiedId
  | QualifiedId (EQUAL ID)? ;

QualifiedId = ID (DOT ID)* ;

///////////////////////////////////////////////////////////////////////////////
// PARAMETERS

Parameter = Type? ID (EQUAL Expression)? ;

Parameters = Parameter (COMMA Parameter)* ;

///////////////////////////////////////////////////////////////////////////////
// PORT DECLARATIONS

PortDecl = TOK_MULTI? Type? ID ;

PortDecls = PortDecl (COMMA PortDecl)* ;

///////////////////////////////////////////////////////////////////////////////
// TYPES

Type = ID TypeRest? ;

TypeAttr = ID TypeAttrRest ;

TypeAttrs = TypeAttr (COMMA TypeAttr)* ;

TypeAttrRest = COLON Type
  | EQUAL Expression ;

TypePar = ID (LT Type)? ;

TypePars = TypePar (COMMA TypePar)* ;

TypeRest = LBRACKET TypePars? RBRACKET
  | LPAREN TypeAttrs? RPAREN ;

///////////////////////////////////////////////////////////////////////////////
// EXPRESSIONS

Expression = Factor (Operator Factor)* ;

Operator = PLUS | MINUS | TIMES | DIV ;

Factor = Atom
  | LPAREN Expression RPAREN ;

Atom = ID | NUMBER | STRING ;

///////////////////////////////////////////////////////////////////////////////
// VARIABLE DECLARATION

VarDeclSection = TOK_VAR VarDecl+ ;

VarDecl = TOK_MUTABLE? Type? ID ((EQUAL | COLON_EQUAL) Expression)? SEMICOLON ;

///////////////////////////////////////////////////////////////////////////////
// ENTITY DECLARATION

EntitySection = TOK_ENTITIES EntityDecl+ ;

EntityDecl = ID EQUAL EntityExpr SEMICOLON ;

EntityExpr = ID LPAREN EntityPars? RPAREN ;

EntityPars = EntityPar (COMMA EntityPar)* ;

EntityPar = ID EQUAL Expression ;

///////////////////////////////////////////////////////////////////////////////
// STRUCTURE DECLARATION

StructureSection = TOK_STRUCTURE StructureStmt+ ;

StructureStmt = Connector DOUBLE_DASH_ARROW Connector AttributeSection? SEMICOLON ;

Connector = ID (DOT ID)? ;

///////////////////////////////////////////////////////////////////////////////
// ATTRIBUTE SECTION

AttributeSection = LBRACE AttributeDecl* RBRACE ;

AttributeDecl = ID EQUAL Expression SEMICOLON
  | ID COLON Type SEMICOLON ;
